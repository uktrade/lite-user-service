buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'maven-publish'

group 'uk.gov.bis.lite'

project.version = project.hasProperty('projVersion') ? project.projVersion : '1.0-SNAPSHOT'

mainClassName = "uk.gov.bis.lite.user.UserServiceApplication"

ext.dropwizardVersion = '1.2.2'

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases/"
  }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-client:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:4.1.0'
  compile 'net.logstash.logback:logstash-logback-encoder:4.10'
  compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'
  compile 'com.github.toastshaman:dropwizard-auth-jwt:1.1.2-0'

  // Lite dependencies
  compile 'uk.gov.bis.lite:spire-client:1.6'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'uk.gov.bis.lite:json-console-appender:1.1'
  compile 'uk.gov.bis.lite:readiness-metric:1.0'
  compile 'uk.gov.bis.lite:jwt:1.2'
  compile 'uk.gov.bis.lite:paas-utils:1.0'

  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.10.0'
  testCompile 'org.assertj:assertj-core:3.8.0'
  testCompile "com.github.tomakehurst:wiremock:2.14.0"
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'src/main/resources/config.yaml' }

def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
  }
}


task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/user/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/user/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-user-service-api'
      version '1.0'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
}